buildscript {
    ext.jaxrs_functional_test_builder_version = "1.0.4"
}

plugins {
    id 'org.jetbrains.kotlin.jvm' version "1.3.72"
    id "org.jetbrains.kotlin.plugin.allopen" version "1.3.72"
    id "org.jetbrains.kotlin.kapt" version "1.4.30"
    id "org.openapi.generator" version "5.0.1"
    id 'io.quarkus'
}

repositories {
    maven { url 'http://download.osgeo.org/webdav/geotools/' }
    maven { url 'https://repo.osgeo.org/repository/release/' }
    mavenLocal()
    mavenCentral()
}

dependencies {
    implementation 'io.quarkus:quarkus-keycloak-admin-client'
    implementation 'io.quarkus:quarkus-liquibase'
    implementation 'io.quarkus:quarkus-jdbc-mysql'
    implementation 'io.quarkus:quarkus-hibernate-orm'
    implementation enforcedPlatform("${quarkusPlatformGroupId}:${quarkusPlatformArtifactId}:${quarkusPlatformVersion}")
    implementation 'io.quarkus:quarkus-resteasy-jackson'
    implementation 'io.quarkus:quarkus-kotlin'
    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
    implementation 'io.quarkus:quarkus-arc'
    implementation 'io.quarkus:quarkus-resteasy'
    implementation 'io.quarkus:quarkus-oidc'

    implementation "org.geotools:gt-referencing:24.0"
    implementation "edu.ucar:netcdf:4.2"
    implementation "org.apache.commons:commons-csv:1.8"
    
    testImplementation 'io.quarkus:quarkus-junit5'
    testImplementation 'org.testcontainers:junit-jupiter:1.15.1'
    testImplementation 'org.testcontainers:mysql:1.15.1'
    testImplementation 'com.github.dasniko:testcontainers-keycloak:1.5.0'
    testImplementation 'com.squareup.okhttp3:okhttp'
    testImplementation 'io.quarkus:quarkus-junit5'
    testImplementation "com.squareup.moshi:moshi-kotlin:1.9.2"
    testImplementation "com.squareup.moshi:moshi-adapters:1.9.2"
    testImplementation "fi.metatavu.jaxrs.testbuilder:jaxrs-functional-test-builder:$jaxrs_functional_test_builder_version"

    kapt "org.hibernate:hibernate-jpamodelgen:5.4.27.SP1"
}

group 'fi.metatavu.megasense.dataportal'
version '1.0.0-SNAPSHOT'

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}

sourceSets.main.java.srcDirs += ['build/generated/api-spec/src/gen/java']
sourceSets.test.java.srcDirs += ['build/generated/api-client/src/main/kotlin']

allOpen {
    annotation("javax.ws.rs.Path")
    annotation("javax.enterprise.context.ApplicationScoped")
    annotation("io.quarkus.test.junit.QuarkusTest")
}

task generateApiSpec(type: org.openapitools.generator.gradle.plugin.tasks.GenerateTask) {
    generatorName = "jaxrs-spec"
    inputSpec = "$rootDir/megasense-data-portal-api-spec/swagger.yaml".toString()
    outputDir = "$buildDir/generated/api-spec".toString()


    configOptions = [
            dateLibrary: "java8",
            interfaceOnly: "true",
            returnResponse: "true",
            useSwaggerAnnotations: "false"
    ]

    apiPackage = "fi.metatavu.megasense.dataportal.api.spec"
    invokerPackage = "fi.metatavu.megasense.dataportal.api.spec.invoker"
    modelPackage = "fi.metatavu.megasense.dataportal.api.spec.model"
    configOptions = [
            dateLibrary: "java8",
            interfaceOnly: "true",
            returnResponse: "true",
            useSwaggerAnnotations: "false"
    ]
}

task generateApiClient(type: org.openapitools.generator.gradle.plugin.tasks.GenerateTask) {
    generatorName = "kotlin"
    library = "jvm-okhttp3"
    inputSpec = "$rootDir/megasense-data-portal-api-spec/swagger.yaml".toString()
    outputDir = "$buildDir/generated/api-client".toString()

    packageName = "fi.metatavu.megasense.dataportal.api.client"
    configOptions = [
            dateLibrary: "string"
    ]
}

compileKotlin {
    dependsOn generateApiSpec
    kotlinOptions.jvmTarget = JavaVersion.VERSION_11
    kotlinOptions.javaParameters = true
}

compileTestKotlin {
    dependsOn generateApiClient
    kotlinOptions.jvmTarget = JavaVersion.VERSION_11
}

kapt {
    correctErrorTypes = true
}